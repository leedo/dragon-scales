#!/usr/bin/env perl

use v5.14;

use Getopt::Long;
use AnyEvent::rrdcached;
use Twiggy::Server;
use Plack::Builder;
use Plack::Request;

my $dir;
my %twiggy = (host => "0.0.0.0", port => 5000);

GetOptions(
  "cachedir=s" => \$dir,
  "listen=s" => \($twiggy{listen}),
  "port=s"   => \($twiggy{port}),
  "host=s"   => \($twiggy{host}),
);

my $rrdcached = AnyEvent::rrdcached->new($dir);
$rrdcached->spawn->recv;

my $app = builder {
  mount "/dragon" => sub {
    my $req = Plack::Request->new(shift);

    sub {
      my $respond = shift;
      my $id = $req->parameters->{id};
      my @stats = $req->parameters->get_all("stats");
      my $time = time;

      for (@stats) {
        $rrdcached->update("$id-$_.rrd", $time, 1, sub {
          my ($res, $err) = @_;
          warn $err if $err;
        });
      }

      $respond->([200, ["Content-Type", "text/plain"], ["ok"]]);
    };
  };
};

Twiggy::Server->new(%twiggy)->run($app);
