#!/usr/bin/env perl

use v5.14;

use Getopt::Long;
use AnyEvent::rrdcached;
use Twiggy::Server;
use Plack::Builder;

my $dir;
my %twiggy = (host => "0.0.0.0", port => 5000);

GetOptions(
  "cachedir=s" => \$dir,
  "listen=s" => \($twiggy{listen}),
  "port=s"   => \($twiggy{port}),
  "host=s"   => \($twiggy{host}),
);

my $rrdcached = AnyEvent::rrdcached->new($dir);
$rrdcached->spawn->recv;

my $app = builder {
  mount "/dragon" => sub {
    my $env = shift;
    sub {
      my $respond = shift;
      $rrdcached->command("STATS")->cb(sub {
        my $lines = eval { shift->recv };

        if ($@) {
          $respond->([500, ["Content-Type", "text/plain"], [$@]]);
          return;
        }

        $respond->([200, ["Content-Type", "text/plain"], [ join "\n", @$lines]]);
      });
    };
  };
};

Twiggy::Server->new(%twiggy)->run($app);
